// This buildscript{} block configures the code driving the build
buildscript {
   /**
    * The nested repositories{} block declares that this build uses the
    * jcenter repository.
    */
    repositories {
        jcenter()
        google()
    }

   /**
    * This block declares a dependency on the 3.2.1 version
    * of the Gradle plugin for the buildscript.
    */
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

/**
 * This line applies the com.android.application plugin. Note that you should
 * only apply the com.android.application plugin. Applying the Java plugin as
 * well will result in a build error.
 */
apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

/**
 * This dependencies block includes any dependencies for the project itself. The
 * following line includes all the JAR files in the libs directory.
 */
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:support-v4:+'
    implementation 'org.jsoup:jsoup:1.11.3'
}

/**
 * The android{} block configures all of the parameters for the Android build.
 * You must provide a value for at least the compilation target.
 */
android {
    compileSdkVersion 28

    /**
    * This nested sourceSets block points the source code directories to the
    * existing folders in the project, instead of using the default new
    * organization.
    */

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 26
    }
    flavorDimensions "version"
    productFlavors {
        amiko {
            // Assigns this product flavor to the "version" flavor dimension.
            // This property is optional if you are using only one dimension.
            dimension "version"
            applicationIdSuffix ".de"
            versionNameSuffix "-de"
        }
        comed {
            dimension "version"
            applicationIdSuffix ".fr"
            versionNameSuffix "-fr"
        }
    }

    def amikoAssetDir = new File(projectDir, 'src/amiko/assets')
    task downloadAmikoDB(type: Download) {
        src "http://pillbox.oddb.org/amiko_db_full_idx_de.zip"
        dest new File(buildDir, 'amiko_db_full_idx_de.zip')
        overwrite false
        onlyIfModified true
    }
    task unzipAmikoDB(dependsOn: downloadAmikoDB, type: Copy) {
        from zipTree(downloadAmikoDB.dest)
        into amikoAssetDir
    }
    task downloadAmikoReport(type: Download) {
        src "http://pillbox.oddb.org/amiko_report_de.html"
        dest amikoAssetDir
    }
    def comedAssetDir = new File(projectDir, 'src/comed/assets')
    task downloadComedDB(type: Download) {
        src "http://pillbox.oddb.org/amiko_db_full_idx_fr.zip"
        dest new File(buildDir, 'amiko_db_full_idx_fr.zip')
        overwrite false
        onlyIfModified true
    }
    task unzipComedDB(dependsOn: downloadComedDB, type: Copy) {
        from zipTree(downloadComedDB.dest)
        into comedAssetDir
    }
    task downloadComedReport(type: Download) {
        src "http://pillbox.oddb.org/amiko_report_fr.html"
        dest comedAssetDir
    }
    tasks.whenTaskAdded { task ->
        if (task.name == "generateAmikoDebugResources" || task.name == "generateAmikoReleaseResources") {
            task.dependsOn unzipAmikoDB
            task.dependsOn downloadAmikoReport
        } else if (task.name == "generateComedDebugResources" || task.name == "generateComedReleaseResources") {
            task.dependsOn unzipComedDB
            task.dependsOn downloadComedReport
        }
    }
}

repositories {
    google()
    jcenter()
}
